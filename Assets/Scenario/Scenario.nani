
#Replay
@print "А ведь всё могло быть совсем не так. Попробовать еще раз?" if:ends>0&ends<4
@print "Вы собрали все концовки. Поздравляем! Возьмите с полки пирожок с лосятиной." if:ends>=4
@choice "История первая. Осколки"
@choice "История вторая. Кай и Герда" goto:.Story2 if:st2
@choice "История третья. Начало пути" goto:.Story3 if:st3
@choice "История четвертая. Вороны и принцы" goto:.Story4 if:st4
@choice "История пятая. Разбойники" goto:.Story5 if:st5
@choice "История шестая. Лапландия" goto:.Story6 if:st6
@choice "История Седьмая. Замок Снежной пани" goto:.Story7 if:st7
@stop

@printer Dialogue
#Story1
Давным-давно в далекой-далекой галлактике...
@back mountain
Жил-был Тролль, злой-презлой, сущий дьявол, и был он колдуном.
@char Troll pos:20,60
@printer Bubble appearance:Left pos:50,60
Troll: Му-ха-ха!
@printer Dialogue
Однажны утром в понедельник был он в особенно хорошем настроении и смастерил из подручных средств ужасный артефакт. Всякий, кто хоть раз смотрел сквозь него на мир, видел в этом мире только самое плохое.
@char Student pos:80,60
Student: Учитель, зачем вы повесили на меня эту банку?
Troll: Это не просто банка, это очень мощный артефакт, неуч! Отнеси его на вершину самой высокой горы и поставь там. Он будет сверкать на солнце и все увидят его.

@set tr1=false
@set tr2=false

#TrollChoice
@choice "Но я ещё не завтракал!" goto:.Troll1 if:!tr1

@choice "А зачем вам такое злодейство?" goto:.Troll2 if:!tr2
@choice "Да, учитель." goto:.Troll3
@stop

#Troll1
@set tr1=true
Troll: Молчать! Не сметь оспаривать мои приказы! Как вернёшься - съешь кактус. А теперь иди.
@goto .TrollChoice

#Troll2
@set tr2=true
Troll: Все, кого коснется луч света от этого артефакта, получат проклятье Вечного Понедельника. И вообще, не твоё дело. Топай на гору!
@goto .TrollChoice

#Troll3
@hide Troll remove:true
@char Student pos:50,75
Ученик карабкался на гору три дня и три ночи. Наверху было очень холодно и даже рассол в артефакте замерз. 
@char Student pos:20,90
А когда до вершины оставалось уже рукой подать, ученик оступился и уронил свою ношу в пропасть.
@char Student pos:10,60 rotation:,,90 time:.3
@char Student pos:0,20 rotation:,,180 time:.3
Артефакт разбился и разлетелся зелеными осколками по всему миру. Кому-то в бровь, а кому-то в глаз.
@hideAll

@choice "История вторая. Кай и Герда"
@stop

#Story2
@set st2=true
@hideAll
@back village
@save

В одном небольшом городе, в двух соседних домах жили двое бедных детей, Кай и Герда. 
@char Kay pos:20,40 scale:1
@char Gerda pos:80,40
Кай дергал Герду за косички, за что Герда лупила его портфелем. Можно сказать, крепко дружили они с самого детства.
Однажды в глаз Кая залетел осколок мерзлого огурца из разбившегося артефакта, но мальчик и так был оторви-и-выброси, поэтому никто не заметил изменений.
@back izba
@char Kay pos:35,40
@char Gerda pos:65,40
Kay: Эй, вредина, пойдем кататься на санках!
Gerda: Там же метель, олух!
Kay: Говорят, в такую погоду можно увидеть на небе особенно черную тучу, от неё еще слышен шум, как будто пропеллер работает.
Gerda: Да-да, а на той туче сидит Снежная Пани и самых непослушных детей забирает с собой. Детские сказочки.
Kay: Ясное дело, никто в такое не поверит. Ну что, идешь на горку?
@choice "У меня домашка не сделана."
@choice "А без меня слабо?"
@choice "Голова болит, давай в другой раз."
@stop
Kay: Ну и сиди тут одна, зубрила! А я пошел гулять.
@hide Kay
Gerda: Ну и вали! Надеюсь, тебя завалит снегом, чтобы не возвращался до самой весны!
@hideAll

@back metel
@char Kay pos:80,60
Кай назло дошел сквозь метель до горки, затащил на неё санки и даже съехал один раз. 
@char Kay pos:20,40 easing:EaseOutQuad time:1.5
Вдруг он услышал над головой шум пропеллера.
Kay: Ого, какая темная туча! И как низко летит! А что это за веревка с неё свисает?

Мальчик привязал санки к веревке и радостно закричал, когда туча потянула его за собой, разгоняясь всё сильнее.
@char Kay pos:30,50 scale:.5 time:.5
@char Kay pos:10,60 scale:.25 time:.5
@char Kay pos:30,70 scale:.05 time:.5
@hide Kay
@char Queen pos:50,60
Вокруг мела метель, и никто не заметил в туче иллюминатор, а за ним - зловещую женскую фигуру.
@hideAll

@choice "История третья. Начало пути"
@stop

#Story3
@set st3=true
@hideAll
@back izba
@save

@char Gerda pos:80,40
Прошел день, затем неделя, потом другая. Кай так и не появился. 
Пришла весна. Герда дочитала последний том манги и вспомнила про Кая.
@printer Bubble appearance:Right pos:50,60
Gerda: О, шикарный повод срулить из дома. Пойду-ка искать Кая, а там, глядишь, и с принцем каким познакомлюсь...
@printer Dialogue
@back garden
@char Magess pos:20,60 scale:.5
Герда шла целый день и к вечеру остановилась возле большого сада, в котором за кустом клубники сидела подозрительная старушка.
@choice "Добрый день, бабушка! Как здоровье?" goto:.Magess1
@choice "Здрасьте! У вас сгущенка есть на продажу?" goto:.Magess2
@choice "Эй бабка, вылезай, я тебя вижу!"  goto:.Magess3
@stop

#Magess1
@char Magess pos:20,40 scale:1
Magess: Здравствуй, внучка! Умаялась с дороги? Проходи в дом, кваску налью.
Gerda: Кваску - это хорошо. Но я только на минутку зайду, мне некогда.
Magess: Конечно. Вот, выпей. Расскажи мне, куда ты так спешишь?
@goto .LiveWithMagess

#Magess2
@char Magess pos:20,40 scale:1
Magess: Сгущенка есть, грибочки, яблочки. Только у тебя же денег нет, по глазам вижу.
Gerda: Но глазки-то красивые? Вот и угостите яблочком.
Magess: Ладно, проходи. Будет тебе яблочко, только расскажи, куда ты путь держишь.
@goto .LiveWithMagess

#Magess3
@char Magess pos:20,40 scale:1
Magess: Тфу ты, малявка некультурная! Не прячусь я, а крота караулю.
Gerda: Вы охотитесь на крота? А можно с вами?
Magess: Можно, только пойдем сначала в дом, рукавицы тебе дам. А ты расскажи пока, куда идёшь.

#LiveWithMagess
Хозяйка была гостеприимной и быстро уговорила девочку остаться и отдохнуть до завтра.
На следующий день они увлеклись ловлей крота, потом вместе пекли пирог из мертвого лося, а потом Герда забыла куда шла.

@set age=10


#PlusYear
@set age+=1
@goto .MagessEnd if:age>=16
@hide Gerda
@hide Magess
@hidePrinter 
@back tint:#000 time:1
@back tint:#fff time:1
Прошел год.
@char Magess pos:80,40 
@char Gerda pos:20,40 look:right
Magess: Доброе утро, внучка! Сегодня тебе исполняется {age}. Задуй свечки на торте!
Gerda: Ура, я уже совсем взрослая! Ф-ф-ф.. Все задула!
Magess: Вот молодец! Что же тебе подарить на день рождения?
@choice "Большую куклу" 
@choice "Новое платье"
@choice "Красную помаду"
@choice "Веревку покрепче"
@choice "Молоток" goto:.MagessExit
@choice "Газонокосилку"
@stop
Magess: Отличный выбор. Я так и знала. Вот, держи коробку, открой.
Gerda: Спасибо, бабушка!
@goto .PlusYear

#MagessEnd
Герда прожила у старушки так долго, что уже ничто не могло вернуть ей память. Постепенно ведьма передала ей свои знания, а сама тихо скончалась. 
Молодая ведьма получила в наследство и дом, и сад, и промысел. Целыми днями она возилась в саду и поджидала доверчивых путников. Так и состарилась.
@set ends+=1 if:!end_magess
@set end_magess=true
@save
@hideAll
@goto .Replay

#MagessExit
Magess: И зачем тебе молоток?
Gerda: Пойду в сарай, соберу что-нибудь из старых досок.
Magess: И что же из них можно собрать?
Gerda: Да хоть телегу. Ну, или хотя бы санки... Санки? Кай!
Gerda: Бабка! Ты мне всю голову задурила, ведьма!
Magess: Ну ведьма, и что? Скучно мне одной, понимаешь?
Gerda: Иди крота лови, если скучно! А я ухожу искать Кая.
@hideAll

@choice "История четвертая. Вороны и принцы"
@stop

#Story4
@set st4=true
@hideAll
@back forest
@save

@char Gerda pos:70,40 look:right
Герда шла через лес и вдруг услышала, как за спиной треснула ветка.
@char Gerda pos:80,40 look:left
@char Voron pos:20,60
Voron: Кар!
Gerda: Ты кто?
Voron: Я Ворон.
Gerda: Эм... Ты себя в зеркале видел?
Voron: А ты видела зеркала в лесу?
Voron: Шучу. Да, я не птица. Родился волком, а потом меня покусала бешенная ворона. Вот и выросли крылья.
Gerda: Так Ворон - это прозвище!
Voron: Точно. Ты куда идешь такая аппетитная?
Gerda: Я ищу друга. Его зовут Кай. Ты не видел его?
Voron: Здесь за лесом живет принцесса. Недавно к ней приехал молодой парень и они поженились.
Gerda: А на чем он приехал?
Voron: На санях, запряженных...
Gerda: К лешему подробности, это точно он! Проведи меня туда!
Voron: Я дружу с Вороной, которая живёт у них в замке. Могу отвести.
@char Voron pos:120,60 time:.8
@char Gerda pos:120,40 time:.2

@char Voron pos:-20,60 time:0
@char Gerda pos:-20,40 time:0 look:right

@char Voron pos:45,60 time:.25
@char Gerda pos:20,40 time:.2

@back castle
@char Vorona pos:80,40
Gerda: Так это и есть бешеная ворона, которая тебя покусала?
Voron: Ну... А как ты догадалась?
Gerda: Не важно. Уважаемая Ворона, вы не могли бы провести меня к принцу, только чтобы стража не увидела?
Vorona: Здравствуй, девочка. Я смотрю, тебя тоже кто-то покусал, если ты пристаешь с такими просьбами к незнакомым... птицам.
Gerda: Думайте что хотите. Мне нужно увидеть принца. То есть Кая. Я его давно ищу.
@hide Voron remove:true
Ворона согласилась и повела Герду в замок. 
@back corridor tint:#333
@char Vorona pos:60,50
@char Gerda pos:30,40
Она завела девочку в темное помещение и уже собиралась связать и покусать её, но вдруг вспыхнул свет.
@back corridor tint:#fff
@hide Vorona remove:true
@char Prince pos:85,40
В дверях стоял принц. Он отчего-то проснулся посреди ночи и решил прогуляться в сторону винного погреба, а тут гости.
К сожалению, это был не Кай. Хотя, принц тоже был очень даже ничего. Герда даже задумалась, а нужен ли ей Кай, но тут появилась принцесса.
@char Princess pos:60,40
Она тоже проснулась посреди ночи и хотела прогуляться к колбасному погребу, а тут гости. И принц.
Princess: Что здесь происходит?
Gerda: Я самая несчастная девочка в мире! Принцы все заняты, а Кая нигде нет.
Герда сквозь слезы рассказала принцу и принцессе свою историю и они почти не смеялись.
Prince: А что ты будешь делать, когда найдешь своего Кая?
@choice "Обниму его, расцелую и отведу домой"
@choice "Помашу ручкой и пойду дальше навстречу приключениям" goto:.PrinceEnd
@choice "Дам ему сковородкой по лбу, чтобы не уходил больше"
@choice "Натравлю на него вашу ворону. С крыльями веселее"
@choice "Убью его, приведу некроманта, воскрешу и снова убью"
@stop

@goto .PrinceNext

#PrinceEnd
Prince: Что ж, мне это нравится! Дорогая, я подаю на развод.
Princess: Я так и знала! Вы давно тайно встречаетесь по ночам! О, горе мне!
@hide Princess
Prince: Герда, позволь мне пойти с тобой. Я подарю тебе звезду. Светом нетленным будет она освещать наш путь в бесконечность!
Gerda: Я согласна, мой принц! Надеюсь, половина королевства после развода останется у тебя?
@char Gerda pos:35,40
@char Prince pos:65,40
Герда и принц взялись за руки и вместе ушли навстречу приключениям. А про Кая больше никто не вспоминал.
@set ends+=1 if:!end_prince
@set end_prince=true
@save
@hideAll
@goto .Replay

#PrinceNext
Prince: Мы просто обязаны помочь этой бедняжке.
Princess: Конечно! Это шоу должно продолжаться. Мы дадим ей карету и припасов, чтобы хватило на следующий сезон.

@hideAll
@choice "История пятая. Разбойники"
@stop

#Story5
@set st5=true
@hideAll
@back road
@save

На следующий день Герда уже ехала в карете и тяжело вздыхала, вспоминая принца.
@char Gerda pos:-20,40 look:right
@char Gerda pos:20,40 time:1
На третий день путешествия карета внезапно остановилась. На дорогу выскочили разбойники. 
@char Robber pos:80,30
@char RobberGirl pos:80,65
Главарь держал в руках огромную дубину, а на голове у него сидела маленькая девочка с острым кинжалом.
Robber: И чего ты рассселась в моей карете?
Gerda: Так принцесса и принц были не настоящие? Они ограбили вас и выгнали жить в лесу? Ваше величество...
Robber: Что? Я говорю, вылазь быстрее. Карета теперь моя. Лошади и все припасы - тоже.
RobberGirl: А девочка теперь моя! Она будет моей куклой. Я буду её кормить и гулять.
Robber: Конечно, милая. Только не грызи больше моё ухо.
@hide Robber remove:true
@char RobberGirl pos:70,40
Маленькая разбойница тут же схватила Герду за руку и повела в лес.
RobberGirl: Как тебя зовут?
Gerda: Я Герда.
RobberGirl: Герда, какое холодное оружие ты любишь? Хочешь я покажу тебе папин тайник? Пойдем играть в куклы? 

@set ro1=false
@set ro2=false
@set ro4=false
#RobberChoice
@choice "Давай поиграем в куклы" goto:.Robber1 if:!ro1
@choice "Я хочу посмотреть тайник" goto:.Robber2 if:!ro2
@choice "Мне нравится холодное оружие" goto:.Robber3
@choice "Я не могу стать твоей куклой" goto:.Robber4 if:!ro4
@stop

#Robber1
@set ro1=true
Разбойница очень обрадовалась.
RobberGirl: Значит ты согласна? Отлично! Ты будешь разбойница, тебя поймали стражники, а я буду палач, и я тебя пытаю.
@goto .RobberChoice

#Robber2
@set ro2=true
Разбойница остановилась и нахмурилась.
RobberGirl: Папа говорил, что есть люди, которые любят золото больше своей жизни, а я не верила. Я покажу тайник, но после этого убью тебя.
@goto .RobberChoice


#Robber4
@set ro4=true
RobberGirl: Не можешь? Ты ведь не пробовала!
Gerda: Я уже почти взрослая, а ты маленькая. Я не могу стать твоей куклой.
RobberGirl: О, так ты бесполезная. Тогда я отдам тебя папиным дружкам.
@goto .RobberChoice

#Robber3
@set ro3=true
RobberGirl: Какое именно?
Gerda: Кинжалы, как у тебя.
@char RobberGirl pos:50,45
Кинжал тут же оказался возле горла Герды.
RobberGirl: Врёшь.
Gerda: Ну, на самом деле мне всё равно, лишь бы холодное.
@char RobberGirl pos:55,40
RobberGirl: В смысле?
Gerda: У меня болит голова и хочется приложить что-нибудь холодненькое.
RobberGirl: А ты мне нравишься! Рассказывай, что ты делала в этом в лесу.
Герда рассказала маленькой разбойнице свою историю и та тут же захотела помочь.
RobberGirl: Твоего Кая наверняка похитила Снежная Пани. Но она живет где-то далеко, в Лапландии.
RobberGirl: Я отдам тебе своего любимого ездового оленя и он отвезёт тебя туда.
@char Deer pos:80,40
Deer: Я не олень!
@char RobberGirl pos:80,40
@char Deer pos:50,40
RobberGirl: Держи уздчку и плётку. Он немного строптивый, но ты точно справишься. Удачи.
Gerda: Так вот ты какой, Северный олень. Ну, поехали. Спасибо, подружка!
Deer: Но я не олень!
Gerda: Хочешь остаться здесь и поговорить об этом?
Deer: Эм... нет. Я Северный олень. Я с удовольствием отвезу тебя в Лапландию.

@hideAll

@choice "История шестая. Лапландия"
@stop

#Story6
@set st6=true
@hideAll
@back road
@save

@char Deer pos:30,40
@char Gerda pos:70,40 look:left
Deer: По сценарию, мы должны поехать сначала к Лапландке. Она выслушает наши истории, потом напишет записку и отправит к Финке. И только Финка подскажет путь в замок Снежной Пани.
Gerda: У меня есть сушеная треска, припрятала от разбойников на черный день. Я нацарапаю на ней что-нибудь. Поехали сразу к Финке.
Deer: А может, перейдем сразу к последней главе? Герда босиком идет к замку Снежной Пани, а Северный олень провожает её со слезами на глазах.
Gerda: Дурацкий план. Нет уж, назвался оленем - вези до Финки.

@back finland
@char Gerda pos:20,40 look:right
@char Deer pos:50,45
@char Finka pos:80,40
Через несколько дней усталые путники добрались до землянки старой Финки.
Deer: Здравствуй, мудрая женщина!...
Finka: Это я должна была стать оленем.
Deer: Простите, что?
Finka: Ты захапал мою роль, а теперь изображаешь тут вежливого оленя. Брысь отсюда!
@hide Deer
Finka: А ты заходи в дом.
@char Gerda pos:30,40 look:right
Gerda: Здравствуй, мудрая женщина! Ты не поможешь мне найти Кая?
Finka: А ты ничего не забыла?
Gerda: Конечно. Вот записка от твоей подруги, Лапландки.
Финка тут же сунула таранку в рот и довольно захрустела.
Finka: Узнаю почерк своей подруги. Так чем тебе помочь?
Gerda: Мне нужно попасть в замок Снежной Пани и спасти своего друга.
Finka: Её дирижабль недавно пролетел в сторону Московии. Видимо, на сходку опять. Два дня её в замке не будет.
Gerda: Я так и знала, что это не просто облако!
Finka: А вот твой Кай наверняка уже стал овощем после стольких лет в её замке. Отмороженным овощем.

@choice "Я всё равно хочу попробовать спасти его."
@choice "Догадываюсь. Надо добить его, чтоб не мучался"
@choice "Он таким и был. Схожу проверю"
@stop

Finka: Система безопасности замка не пустит тебя. Только если разуешься и притворишься белочкой, потому что все обутые считаются врагами.
@choice "Белочкой? Я не готова на такой подвиг" goto:.Finka1
@choice "Хорошо. Присмотри за сапогами, пожалуйста" goto:.Finka2
@stop

#Finka1
Finka: Ну хоть кто-то здравомыслящий попался. Вот, выпей зелья и все твои проблемы исчезнут.
Gerda: Здравомыслящий не будет пить твои зелья.
Finka: Какой умный ребёнок. А жаль, мне очень нужен новый питомец. Тогда снимай сапоги и рукавицы, и иди на все четыре.
@goto .FinkeEnd

#Finka2
Finka: И рукавицы снимай. Белочки не носят рукавиц.
Gerda: Спасибо тебе, мудрая жещина. Укажи мне путь во тьме!
Finka: Ты переигрываешь. Олень отвезет тебя к замку Снежной пани, не успеешь и моргнуть.

#FinkeEnd
@hide Finka
@char Gerda pos:20,40 look:right
@char Deer pos:50,40
Герда сняла сапоги и рукавицы и вышла на мороз. 

@hideAll

@choice "История Седьмая. Замок Снежной пани"
@stop

#Story7
@set st7=true
@hideAll
@back snowCastle
@save

@char Deer pos:80,40
@char Gerda pos:50,40 look:right
Не успела девочка моргнуть, как Северный олень довез её до огромного снежного замка.
@hide Deer remove:true
@char Gerda pos:20,40 look:right
@printer Bubble appearance:Left pos:50,60
Gerda: Я белочка. Я меленькая рыжая белочка. 
Gerda: Мне совсем не холодно. Я белочка-горячечка.
@printer Dialogue
Бормоча себе под нос и стуча зубами, девочка дошла до двери в подвал с надписью "Овощи". За ней оказался Кай.
@char Kay scale:1 pos:80,40
Он сидел на полу босой и пытался сложить из кубиков с буквами "А", "О", "П" и "Ж" слово "вечность", как повелела хозяйка.

@set ka1=false

#KayChoice
@choice "Обнять Кая и обрыдаться" goto:.Kay1 if:!ka1
@choice "Развернуться и уйти домой" goto:.Kay2
@choice "Рявкнуть и влепить Каю подзатыльник" goto:.Kay3
@stop

#Kay1
@set ka1=true
@char Gerda pos:50,40 look:right
Герда обняла Кая и заплакала. К сожалений, это ничем не помогло. Мальчик никак не реагировал.
@goto .KayChoice

#Kay2
@hide Kay
@char Gerda pos:25,40 look:left
Герда развернулась и побежала прочь из замка. К сожалению, она забыла, что она белочка, поэтому охранное волшебство проснулось и сковало её. 
@char Queen pos:50,60
А когда вернулась Снежная пани, в поваренной книге северных народов появилось новое блюдо "Белка, фаршированная сладким перцем".
Так бездарно закончилась история юной спасительницы.
@set ends+=1 if:!end_queen
@set end_queen=true
@save
@hideAll
@goto .Replay

#Kay3
@char Gerda pos:50,40 look:right
Герда закричала и со всей дури стукнула друга по затылку. 
@char Kay pos:20,40
@char Gerda pos:50,40 look:left
Голова дёрнулась и осколок огурца из артефакта Тролля выпал из глаза Кая. 
А потом в оставшуюся дырку высыпались все снежные такаканы, которых запустила Снежная Пани. Кай узнал Герду, они обнялись и вместе ушли в закат.
Ну ладно, всё было не совсем так. Но ругались они не долго и дрались не сильно. А потом всё равно ушли вместе.
Уходя, они хорошенько хлопнули дверью, отчего кубиков стало намного больше и они тут же сложились в нужное слово. Даже в несколько слов.
@set ends+=1 if:!end_true
@set end_true=true
@save
@hideAll
@goto .Replay

@stop